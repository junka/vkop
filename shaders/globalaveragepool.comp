#version 450 core

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

#ifdef FP16
#extension GL_AMD_gpu_shader_half_float: enable
#extension GL_EXT_shader_16bit_storage : enable
#define FLOAT4 f16vec4
#else
#define FLOAT4 vec4
#endif

layout(set=0, binding = 0) writeonly buffer outputBuffer {
    uvec2 data[];
} uBuffer;

layout(set=0, binding = 1) uniform sampler2DArray inputImage;          // [W, N*H, C4]

layout(push_constant) uniform paraBuffer {
    ivec4 inShape;
    int accuracy;
} uParams;

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID);
    int n = coord.x;
    int c4 = coord.z;
    int N = uParams.inShape.x;
    int C = uParams.inShape.y;
    int H = uParams.inShape.z;
    int W = uParams.inShape.w;
    if (n >= N || c4 >= (C + 3) /4) return;
    int numInputs = H * W;

    FLOAT4 sum = FLOAT4(0.0);
    for (int x = 0; x < W; x++) {
        for (int y = 0; y < H; y++) {
            uint total_h = n * H + y;
            FLOAT4 val = FLOAT4(texelFetch(inputImage, ivec3(x, total_h, c4), 0));
            sum += val;
        }
    }

    int index = n * C/4 + c4;
    if (uParams.accuracy == 0) {
        uvec4 result = floatBitsToUint(vec4(sum / float(numInputs)));
        uBuffer.data[index * 2] = result.xy;
        uBuffer.data[index * 2 + 1] = result.zw;
    } else if (uParams.accuracy == 1) {
        vec4 normalized_sum = vec4(sum / float(numInputs));
        uvec2 packed_data = uvec2(0);
        packed_data.x = packHalf2x16(normalized_sum.xy);
        packed_data.y = packHalf2x16(normalized_sum.zw);
        uBuffer.data[index] = packed_data;
    }
}