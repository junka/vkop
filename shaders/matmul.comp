#version 450
layout(local_size_x = 16, local_size_y = 16) in;

layout(set=0, binding = 0) writeonly buffer BufferC {
    float data[];
} bufferC;

layout(set=0, binding = 1) readonly buffer BufferA {
    float data[];
} bufferA;

layout(set=0, binding = 2) readonly buffer BufferB {
    float data[];
} bufferB;

layout(set=0, binding = 3) readonly uniform paraBuffer {
    int M; // 行数
    int N; // 列数
    int K; // 共同维度
} uMatMulParams;

void main() {
    uint j = gl_GlobalInvocationID.x; // column index in C
    uint i = gl_GlobalInvocationID.y; // row index in C

    if (i >= uMatMulParams.M || j >= uMatMulParams.N) return;

    float sum = 0.0;
    for (int k = 0; k < uMatMulParams.K; ++k) {
        uint aIndex = i * uMatMulParams.K + k;   // A[i][k]
        uint bIndex = k * uMatMulParams.N + j;   // B[k][j]
        sum += bufferA.data[aIndex] * bufferB.data[bIndex];
    }

    uint cIndex = i * uMatMulParams.N + j;       // C[i][j] in row-major
    bufferC.data[cIndex] = sum;
}